description = "Apereo CAS Management Web Application Configuration"
def skipConfigurationMetadata = Boolean.getBoolean("skipNestedConfigMetadataGen")

dependencies {
    implementation libraries.springcloudconfigclient
    implementation libraries.casServer

    if (!skipConfigurationMetadata) {
        annotationProcessor libraries.springbootconfigmetadata
    }
}

if (!skipConfigurationMetadata) {
    task generateConfigurationMetadata(type: JavaExec) {
        doFirst {
            logger.info "\nRunning CAS Spring configuration metadata processor in addition to the native " +
                    "Spring Boot metadata processor, to create configuration " +
                    "metadata for nested and indexed properties. The process starts with the build directory " +
                    "at [$buildDir] for the CAS configuration module at [$projectDir.canonicalPath].\n"
        }
        main = "org.apereo.cas.configuration.metadata.ConfigurationMetadataGenerator"
        classpath = sourceSets.main.runtimeClasspath
        args buildDir, projectDir.canonicalPath
    }

    compileJava.dependsOn(processResources)
    jar.dependsOn(generateConfigurationMetadata)
}


