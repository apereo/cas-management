test {
    useJUnitPlatform {
        excludeTags "Cassandra", "CosmosDb", "Couchbase", "DynamoDb",
                "FileSystem", "Ignite", "InfluxDb", "CouchDb",
                "Ldap", "Mail", "MariaDb", "Memcached",
                "MongoDb", "MsSqlServer", "MySQL", "Postgres", "Redis",
                "Radius", "AmazonWebServicesS3", "RestfulApi", "Groovy",
                "OAuth", "OIDC", "SAML"
    }
    enabled = "SIMPLE".equalsIgnoreCase(rootProject.testCategoryType) && !Boolean.getBoolean("skipTests")
}

tasks.withType(Test) {
    scanForTestClasses = false
    ignoreFailures = Boolean.getBoolean("ignoreTestFailures")

    doFirst {
        maxParallelForks = project.ext.properties.get("maxParallelForksForTests", 4)
        if (maxParallelForks != 2) {
            logger.info "Maximum number of parallel forks is set to ${maxParallelForks}"
        }
    }

    systemProperty("spring.main.banner-mode", "off")
    failFast = true
    jvmArgs.addAll(["-Xms512m", "-Xmx4g"])
    classpath = it.classpath + files("${projectDir}/src/main/resources") + files("${projectDir}/src/test/resources")
    testLogging {
        events = ["FAILED", "STANDARD_OUT"]
        exceptionFormat = "full"
        showStandardStreams = Boolean.getBoolean("showStandardStreams")
        showExceptions = true
        showCauses = true
    }
}

def shouldTest(Project project, String category) {
    !Boolean.getBoolean("skipTests") && category.equalsIgnoreCase(rootProject.testCategoryType) && hasTestsOfCategory(project, category)
}

def hasTestsOfCategory(Project project, String category) {
    def testSrc = project.sourceSets.test.java.srcDirs
    def res = srcTagMap.computeIfAbsent(testSrc) {
        def tags = new HashSet()
        testSrc.each { src ->
            try {
                src.eachFileRecurse { file ->
                    if (file ==~ /.*Tests\.\w+/) {
                        file.readLines().parallelStream().forEach() { line ->
                            def m = line =~ /^\s*@Tag\("(\w+)"\)\s*$/
                            if (m) {
                                tags.addAll(m.collect { it[1] })
                            }
                        }
                    }
                }
            } catch (ignored) {
            }
        }
        tags
    }
    res.contains(category)
}
