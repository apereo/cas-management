description = "Apereo CAS Management Web Application"

apply plugin: "war"

project.ext["mainClassName"] = "org.apereo.cas.mgmt.web.CasManagementWebApplication"

sourceSets {
    main {
        resources {
            srcDirs "${project.buildDir}/generated-src/main/resources"
        }
    }
    /**
     * Make resources become reloadable.
     * Applicationâ€™s static resources are loaded from their source location.
     */
    bootRun {
        resources {
            srcDirs new File(project(":webapp").getProjectDir(), "resources")
        }
    }
}

apply from: rootProject.file("gradle/node.gradle")
apply from: rootProject.file("gradle/springboot.gradle")


dependencies {
    implementation libraries.casServer 
    implementation libraries.classmate
    
    implementation project(":config:cas-management-config-services")
    implementation project(":config:cas-management-config-audit")
    implementation project(":config:cas-management-config-authentication")
    implementation project(":config:cas-management-config-authz")
    implementation project(":config:cas-management-config-services")
    implementation project(":core:cas-management-core-configuration")
    implementation project(":webapp:cas-management-webapp-config")
    implementation project(":webapp:cas-management-webapp-init")
    
}

/**
 * Share the resources directory outside the content root of the project
 * for all webapp projects in a dynamic fashion, and do it only when the taskGraph is ready.
 * 
 * This allows the IDE, specially Intellij IDEA, to ignore the resources
 * directory for webapp projects, allowing it to be shared across modules.
 * While the directory isn't recognizes as a "resources" directory by the IDE,
 * it is still a valid legitimate resources directory recognized by Gradle.
 * Note that bootRun also includes the same directory for resources, allowing
 * for automatic updates to static resources, etc.
 */
gradle.taskGraph.whenReady {taskGraph ->
    def resourcesDir = "${project.projectDir}/../resources"
    logger.info("Adding resources directory ${resourcesDir} to project ${project.name}")
    
    sourceSets {
        main {
            resources {
                srcDir resourcesDir
            }
        }
    }
    
}
