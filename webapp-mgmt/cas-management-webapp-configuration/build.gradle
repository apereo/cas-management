description = "Apereo CAS central configuration module providing type-safe configuration domain model to the rest of CAS server"

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
}

dependencies {
    implementation libraries.springsecurity
    implementation libraries.mongo
    implementation libraries.jasypt
    implementation libraries.groovy
    implementation libraries.bouncycastle
    
    optional libraries.springbootconfigmetadata

    implementation "org.apereo.cas:cas-server-core-api-authentication:$casVersion"
    implementation "org.apereo.cas:cas-server-core-api-webflow:$casVersion"
    compileOnly "org.apereo.cas:cas-server-core-util:$casVersion"
    testImplementation "org.apereo.cas:cas-server-core-util:$casVersion"
}

task generateConfigurationMetadata(type: JavaExec) {
    doFirst {
        println "\nRunning CAS Spring configuration metadata processor in addition to the native " +
                "Spring Boot metadata processor, to create configuration " +
                "metadata for nested and indexed properties. The process starts with the build directory " +
                "at [$buildDir] for the CAS configuration module at [$projectDir.canonicalPath].\n"
    }
    main = "org.apereo.cas.configuration.metadata.ConfigurationMetadataGenerator"
    classpath = sourceSets.main.runtimeClasspath
    args buildDir, projectDir.canonicalPath
}

compileJava.dependsOn(processResources)

if (!Boolean.getBoolean("skipNestedConfigMetadataGen")) {
    jar.dependsOn(generateConfigurationMetadata)
}
